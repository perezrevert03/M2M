-- @nsURI uml=dsmUML
-- @nsURI er=MyER

module uml2rdb;
create OUT : er from IN : uml;

rule diagrama2esquema { -- 1. Diagrama a esquema
	from
		d : uml!Diagrama
	to 
		e : er!Esquema (
			Nombre <- d.nombre,
			Tablas <- d.clases, -- 2. De clase a tabla
			clavesAjenas <- d.asociaciones -- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1	
		)
}

rule clase2table { -- 2. De clase a tabla
	from
		c : uml!Clase
	to 
		t : er!Tabla (
			Nombre <- c.nombre,
			columnas <- col, -- 3. Columna id en cada tabla
			columnas <- c.atributos -- 4. Generación de columnas en cada tabla
		),
		col : er!Columna ( -- 3. Columna id en cada tabla
			Nombre <- 'id_' + c.nombre,
			EsPrimaria <- true,
			Tipo <- #integer
		)
}

rule atributo2columna { -- 4. Generación de columnas en cada tabla
	from 
		a : uml!Atributo
	to	
		col : er!Columna (
			Nombre <- a.nombre,
			EsPrimaria <- false,
			Tipo <- if (a.tipo.toString() = 'entero') -- 5. Generación de datos de columnas
				    then #integer 
				    else if (a.tipo.toString() = 'real') 
					     then #double 
				         else #string 
					     endif
				    endif
		)
}

-- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1
rule asociacion2claveajena { 
	from 
	-- Si la relación es de (1 a 1) o de (1 a n)
		a : uml!Asociacion (a.max1 = 1)
	to 
		fk : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol2 + '_a_' + a.rol1,
			columna <- fk_columna, -- columna por referencia
			-- la columna referenciada es la columna id de...
			-- la clase a la que apunta la clave ajena que...
			-- se ha creado anteriormente en la regla clase2table
			columnaReferenciada <- thisModule.resolveTemp(a.clase1, 'col')
		),
		fk_columna : er!Columna (
			Nombre <- 'id_fk_' + a.rol1,
			tabla <- a.clase2
		)
}

-- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1
rule asociacion2claveajena2 {
	from 
	-- Si la relación es de (n a 1)
		a : uml!Asociacion (a.max2 = 1 and a.max1 > 1)
	to 
		fk : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol1 + '_a_' + a.rol2,
			columna <- fk_columna,
			columnaReferenciada <- thisModule.resolveTemp(a.clase2, 'col')
		),
		fk_columna : er!Columna (
			Nombre <- 'id_fk_' + a.rol2,
			tabla <- a.clase1
		)
}

-- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1
rule asociacion2claveajena3 {
	from
	-- Si la relación es de (n a n)
		a : uml!Asociacion (a.max1 > 1 and a.max2 > 1)
	to 
		fk_1 : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol1 + '_a_' + a.rol2,
			columna <- fk_columna_1,
			columnaReferenciada <- thisModule.resolveTemp(a.clase2, 'col')
		),
		fk_2 : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol2 + '_a_' + a.rol1,
			columna <- fk_columna_2,
			columnaReferenciada <- thisModule.resolveTemp(a.clase1, 'col'),
			esquema <- fk_1.esquema
		),
		fk_columna_1 : er!Columna (
			Nombre <- 'id_fk_' + a.rol2,
			tabla <- t
		),
		fk_columna_2 : er!Columna (
			Nombre <- 'id_fk_' + a.rol1,
			tabla <- t
		),
		t : er!Tabla (
			Nombre <- a.rol1 + '_' + a.rol2,
			Padre <- fk_1.esquema
		)
}