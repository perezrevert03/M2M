-- @nsURI uml=dsmUML
-- @nsURI er=MyER

module uml2rdb;
create OUT : er from IN : uml;

rule diagrama2esquema { -- 1. Diagrama a esquema
	from
		d : uml!Diagrama
	to 
		e : er!Esquema (
			Nombre <- d.nombre,
			Tablas <- d.clases, -- 2. De clase a tabla
			clavesAjenas <- d.asociaciones -- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1	
		)
}

rule clase2table { -- 2. De clase a tabla
	from
		c : uml!Clase (c.csuper.oclIsUndefined())
	to 
		t : er!Tabla (
			Nombre <- c.nombre,
			columnas <- col, -- 3. Columna id en cada tabla
			columnas <- c.atributos -- 4. Generación de columnas en cada tabla
		),
		col : er!Columna ( -- 3. Columna id en cada tabla
			Nombre <- 'id_' + c.nombre,
			EsPrimaria <- true,
			Tipo <- #integer
		)
}

-- 7. Generación de FKs en tablas correspondientes a Clases Hijas
rule clasehija2table {
	from
		c : uml!Clase (not c.csuper.oclIsUndefined())
	to 
		t : er!Tabla (
			Nombre <- c.nombre,
			columnas <- col, -- 3. Columna id en cada tabla
			columnas <- c.atributos -- 4. Generación de columnas en cada tabla
		),
		col : er!Columna ( -- 3. Columna id en cada tabla
			Nombre <- 'id_' + c.nombre,
			EsPrimaria <- true,
			Tipo <- #integer,
			enClaveAjena <- fk -- Crea clave ajena
		),
		fk : er!ClaveAjena (
			Nombre <- 'ca_de_' + c.nombre + '_a_' + c.csuper.nombre,
			columna <- fk_col, -- Crea columna clave foranea
			-- la columna referenciada es la clase id del padre creada anteriormente
			columnaReferenciada <- thisModule.resolveTemp(c.csuper, 'col'),
			esquema <- t.Padre -- pertenece al esquema creado
		),
		fk_col : er!Columna (
			Nombre <- 'id_fk_' + c.csuper.nombre,
			tabla <- t -- pertenece a la tabla hija
		)
}

rule atributo2columna { -- 4. Generación de columnas en cada tabla
	from 
		a : uml!Atributo
	to	
		col : er!Columna (
			Nombre <- a.nombre,
			EsPrimaria <- false,
			Tipo <- if (a.tipo.toString() = 'entero') -- 5. Generación de datos de columnas
				    then #integer 
				    else if (a.tipo.toString() = 'real') 
					     then #double 
				         else #string 
					     endif
				    endif
		)
}

-- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1
rule asociacion2claveajena { 
	from 
	-- Relación de (1 a 1) o de (1 a n) sin clase asociación
		a : uml!Asociacion (a.max1 = 1 and a.claseasociacion.oclIsUndefined())
	to 
		fk : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol2 + '_a_' + a.rol1,
			columna <- fk_columna,
			columnaReferenciada <- thisModule.resolveTemp(a.clase1, 'col')
		),
		fk_columna : er!Columna (
			Nombre <- 'id_fk_' + a.rol1,
			tabla <- a.clase2
		)
}

-- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1
rule asociacion2claveajena2 {
	from
	-- Relación de (n a 1) sin clase asociación
		a : uml!Asociacion (a.max2 = 1 and a.max1 > 1 and a.claseasociacion.oclIsUndefined())
	to 
		fk : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol1 + '_a_' + a.rol2,
			columna <- fk_columna,
			columnaReferenciada <- thisModule.resolveTemp(a.clase2, 'col')
		),
		fk_columna : er!Columna (
			Nombre <- 'id_fk_' + a.rol2,
			tabla <- a.clase1
		)
}

-- 6. Generación de FKs correspondientes a asociaciones con multiplicidad > 1
rule asociacion2claveajena3 {
	from
	-- Relación de (n a n) sin clase asociación
		a : uml!Asociacion (a.max1 > 1 and a.max2 > 1 and a.claseasociacion.oclIsUndefined())
	to 
		fk_1 : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol1 + '_a_' + a.rol2,
			columna <- fk_columna_1,
			columnaReferenciada <- thisModule.resolveTemp(a.clase2, 'col')
		),
		fk_2 : er!ClaveAjena (
			Nombre <- 'ca_de_' + a.rol2 + '_a_' + a.rol1,
			columna <- fk_columna_2,
			columnaReferenciada <- thisModule.resolveTemp(a.clase1, 'col'),
			esquema <- fk_1.esquema
		),
		fk_columna_1 : er!Columna (
			Nombre <- 'id_fk_' + a.rol2,
			tabla <- t
		),
		fk_columna_2 : er!Columna (
			Nombre <- 'id_fk_' + a.rol1,
			tabla <- t
		),
		t : er!Tabla (
			Nombre <- a.rol1 + '_' + a.rol2,
			Padre <- fk_1.esquema
		)
}

-- 8. Generación de FKs correspondientes a Clases Asociación
rule asociacion2claveajena4 {
	from
	-- Si la asociación tiene una Clase Asociación...
		a : uml!Asociacion (not a.claseasociacion.oclIsUndefined())
	to 
		fk_1 : er!ClaveAjena ( -- crea clave ajena en una dirección
			Nombre <- 'ca_de_' + a.claseasociacion.nombre + '_a_' + a.rol1,
			columnaReferenciada <- thisModule.resolveTemp(a.clase1, 'col'),
			columna <- fk_columna_1
		), fk_2 : er!ClaveAjena ( -- crea clave ajena en la otra dirección
			Nombre <- 'ca_de_' + a.claseasociacion.nombre + '_a_' + a.rol2,
			columnaReferenciada <- thisModule.resolveTemp(a.clase2, 'col'),
			columna <- fk_columna_2,
			esquema <- fk_1.esquema
		),fk_columna_1 : er!Columna (
			Nombre <- 'id_fk_' + a.rol1,
			tabla <- a.claseasociacion
		), fk_columna_2 : er!Columna (
			Nombre <- 'id_fk_' + a.rol2,
			tabla <- a.claseasociacion
		)
}
